buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
    configurations.maybeCreate('pitest')
    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.4.5'
        pitest "io.kotlintest:kotlintest-plugins-pitest:3.4.2"
        pitest "com.github.pitest:pitest-kotlin:master"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'application'
    id "io.qameta.allure" version "2.8.1"
    id 'info.solidsoft.pitest' version '1.4.5'
}

project.ext {
    kotlinTest = "3.4.2"
}
group 'org.example'
version '1.0.0'
repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation 'com.github.ajalt:clikt:2.3.0'
    testImplementation "io.kotlintest:kotlintest-runner-junit5:${project.kotlinTest}"
    testImplementation "io.kotlintest:kotlintest-extensions-allure:${project.kotlinTest}"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

application {
    mainClassName = 'diffko.MainKt'
}

test {
    useJUnitPlatform()
}

allure {
    autoconfigure = true
    version = '2.13.0'
    configuration = "compileTestKotlin"
}

shadowJar {
    minimize()
    baseName('diffko')
    classifier('all')
    archiveVersion = ''
}

pitest {
    testPlugin = 'KotlinTest'
    targetClasses = ['diffko.*']
    threads = 4
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    maxMutationsPerClass = 0
    mutators = ["NEW_DEFAULTS"]
    avoidCallsTo = ["kotlin.jvm.internal.Intrinsics"]
}
